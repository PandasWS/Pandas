//===== rAthena Script =======================================
//= Jewel Case Quest
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A quest for players at base level 99,
//= with Jewel Case as a reward.
//===== Additional Comments: =================================
//= 1.0 First Version.
//============================================================

lighthalzen,108,229,2	script	La Fenn	766,{
	mes "[La Fenn]";
	if(BaseLevel<99) {
		if(Q_99LVL) {
			mes "You... You got younger!!! But you are definitely YOU!";
			mes "I couldn't think you were able to meet my condition...";
			emotion ET_SCRATCH;
			next;
			mes "[La Fenn]";
			mes "Here is your prize. It's yours for good.";
			set Q_99LVL,0;
			getitem 12106,1; //Jewel Case
			emotion ET_CONGRATULATION;
		} else {
			mes callfunc("F_Hi");
			mes "...";
			emotion ET_SLEEPY;
			next;
			mes "[La Fenn]";
			mes "I've got a riddle for a strong man of 2nd Class and 99 Base Level.";
			mes "*Yawn* If you meet some, please tell them about my riddle.";
		}
		close;
	}
	if(BaseLevel==99 && Upper==0) {
		if(Q_99LVL) {
			mes "Now go forth and get younger!";
			mes "If you solve my riddle, then I'll give you a magical Jewel Case with a rare accessory.";
		} else {
			mes callfunc("F_Hi")+" The Hero!";
			if(BaseExp<1000000) {
				mes "Now you're strong.";
				mes "But... not quite strong for my riddle.";
				if(rand(2)) {
					mes "Kill "+((1000000-BaseExp)/3765+1)+" more Seals and return.";
				} else {
					mes "Kill "+((1000000-BaseExp)/81+1)+" more Poporings and come back.";
				}
			} else {
				mes "Now you're REALLY strong.";
				mes "And if you get younger than now I'll give you my treasure.";
				set Q_99LVL,1;
				emotion ET_BEST;
			}
		}
		close;
	}
	emotion ET_SCRATCH;
	mes "...";
	close;
}
