//===== rAthena Script =======================================
//= Hypnotist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Stat and skill resetter for expanded first class under
//= base level 50 and normal first class.
//===== Additional Comments: =================================
//= 1.0 First version, Renewal script.
//= 1.1 Added Izlude duplicates. [Euphy]
//============================================================

// Main NPC :: resetskill_novice
//============================================================
izlude,127,175,3	script	催眠大師#novice	124,{
	mes "[催眠大師]";
	mes "為了對於自己職業理解不足的人著想,";
	mes "我會免費幫忙讓技能或屬性初始化.";
	next;
	mes "[催眠大師]";
	mes "初始話只限於特定條件之下";
	mes "才能夠提供服務,";
	mes "請依定要確認！～";
	next;
	mes "[催眠大師]";
	mes "初始化服務僅限：^FF0000劍士、服侍、盜賊、^000000";
	mes "^FF0000法師、弓箭手、商人^000000可無限使用本服務.";
	mes "^FF0000跆拳少年/少女、神槍手、忍者^000000的話,";
	mes "則是只有提供給^FF0000未滿50級^000000的角色而已";
	mes "想知道更詳細問題?選擇第一個選項吧！";
	next;
	switch(select("^FF0000確認初始化條件^000000:讓屬性初始化:讓技能初始化:結束對話")) {
	case 1:
		mes "[催眠大師]";
		mes "Stat/Skill Resets allow adventuers";
		mes "to redistribute their Skill";
		mes "Points if they are unhappy";
		mes "with their current skills.";
		next;
		mes "[催眠大師]";
		mes "Before proceeding with";
		mes "a Stat/Skill Reset, you must";
		mes "reduce all of the weight";
		mes "of all carried items on your";
		mes "character to 0. You can put";
		mes "extra items in Kafra Storage.";
		next;
		mes "[催眠大師]";
		mes "Ah, it's also important";
		mes "to remove your ^FF0000Pushcart^000000";
		mes "if you have one equipped.";
		mes "Otherwise, hypnosis won't";
		mes "work, or will backfire...";
		close;
	case 2:
		set .@str$,"Stat";
	case 3:
		if (.@str$ == "")
			set .@str$,"Skill";
		mes "[催眠大師]";
		mes "Are you sure that you";
		mes "want to proceed with";
		mes "my ^FF0000"+.@str$+" Reset^000000 service?";
		next;
		if(select("Yes:Cancel") == 1)
			break;
	case 4:
		mes "[催眠大師]";
		mes "Thank you, and good";
		mes "luck on your adventures.";
		mes "Please travel in safety~";
		close;
	}
	if ((Class >= Job_Swordman && Class <= Job_Thief) || Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja) {
		if (checkcart()) {
			mes "[催眠大師]";
			mes "Oh! Please remove your";
			mes "Pushcart before proceeding";
			mes "with the "+.@str$+" Reset service.";
			mes "Thanks for cooperating~";
			close;
		}
		if (Weight != 0) {
			mes "[催眠大師]";
			mes "If you're here for my "+.@str$;
			mes "Reset service, please";
			mes "remember that you can't";
			mes "reset your "+.@str$+"s until the";
			mes "^FF0000weight of your carried items in";
			mes "your Inventory is reduced to 0^000000.";
			next;
			mes "[催眠大師]";
			mes "Why don't you place your";
			mes "things into the Kafra Storage";
			mes "for now? That way, you can";
			mes "safely keep all of your goods.";
			close;
		}
		if (BaseLevel >= 50 && (Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja)) {
			mes "[催眠大師]";
			mes "I'm sorry, but characters";
			mes "with Base Levels higher";
			mes "than 50 are ineligible for the";
			mes .@str$+" Reset service I provide.";
			close;
		}
		mes "[催眠大師]";
		mes "Thank you for using";
		mes "my "+.@str$+" Redistribution";
		mes "services. Oh, and best";
		mes "of luck to you on your";
		mes "travels, adventurer.";
		if (.@str$ == "Stat")
			resetstatus;
		else
			resetskill;
		close;
	} else {
		mes "[催眠大師]";
		mes "I'm sorry, but your";
		mes "Job Class doesn't qualify";
		mes "for the "+.@str$+" Reset service";
		mes "that I provide. I can only";
		mes "offer "+.@str$+" Resets to the";
		mes "following Jobs...";
		next;
		mes "[催眠大師]";
		mes "^FF0000Swordman, Acolyte,";
		mes "Mage, Thief, Archer,";
		mes "Merchant, Taekwon";
		mes "Boy, Taekwon Girl,";
		mes "Gunslinder and Ninja^000000.";
		close;
	}
}
izlude_a,127,175,3	duplicate(催眠大師#novice)	催眠大師#novice_a	124
izlude_b,127,175,3	duplicate(催眠大師#novice)	催眠大師#novice_b	124
izlude_c,127,175,3	duplicate(催眠大師#novice)	催眠大師#novice_c	124
izlude_d,127,175,3	duplicate(催眠大師#novice)	催眠大師#novice_d	124
